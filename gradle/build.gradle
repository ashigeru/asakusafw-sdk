/*
 * Copyright 2011-2016 Asakusa Framework Team.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
ext.parentPom = { f ->
    if (!f.exists()) {
        return null
    }
    def xml = new XmlSlurper().parse(f)
    return [
        projectVersion : xml.version.text(),
        coreVersion : xml['properties']['asakusafw.version'].text(),
    ]
}(project.file('../pom.xml'))

group = 'com.asakusafw'
version = parentPom.projectVersion

defaultTasks 'clean', 'build', 'install'

apply plugin: 'groovy'
apply plugin: 'maven'
apply plugin: 'idea'
apply plugin: 'eclipse'

configurations {
    deployerJars
    asakusafw
}

repositories {
    if (project.hasProperty('mavenLocal')) {
        logger.lifecycle 'enabling maven local repository'
        mavenLocal()
    }
    mavenCentral()
    maven { url 'http://asakusafw.s3.amazonaws.com/maven/releases' }
    maven { url 'http://asakusafw.s3.amazonaws.com/maven/snapshots' }
}

dependencies {
    compile gradleApi()
    testCompile gradleTestKit()
    testCompile 'junit:junit:4.11'
    asakusafw "com.asakusafw:asakusa-parent:${parentPom.coreVersion}@pom"
    deployerJars 'org.springframework.build:aws-maven:5.0.0.RELEASE'
}

project.sourceCompatibility = JavaVersion.toVersion('1.7')
project.targetCompatibility = JavaVersion.toVersion('1.7')

tasks.withType(AbstractCompile) { task ->
    task.options.encoding = 'UTF-8'
    if (System.getProperty('compiler.bootclasspath') != null) {
        task.options.compilerArgs += ['-bootclasspath', System.getProperty('compiler.bootclasspath')]
    }
}

eclipse.jdt {
    javaRuntimeName = "JavaSE-${sourceCompatibility}"
}

groovydoc {
    docTitle "Asakusa Gradle Plugins ${version}"
    windowTitle "Asakusa Gradle Plugins ${version}"
    footer "Copyright 2011-${new Date().format('yyyy')} Asakusa Framework Team."
}

processResources {
    File outputFile = new File(destinationDir, 'META-INF/asakusa-gradle/artifact.properties')
    inputs.properties parentPom
    outputs.file outputFile
    outputs.files configurations.asakusafw
    doLast {
        logger.info "injecting artifact versions: ${parentPom}"
        if (!outputFile.parentFile.exists()) {
            mkdir outputFile.parentFile
        }

        Properties p = new Properties()
        p.put("plugin-version", parentPom.projectVersion)
        p.put("framework-version", parentPom.coreVersion)
        configurations.asakusafw.each { File pom ->
            if (pom.name.endsWith('.pom')) {
                def props = new XmlSlurper().parse(pom)['properties']
                p.put('slf4j-version', props['slf4j.version'].text())
                p.put('logback-version', props['logback.version'].text())
                p.put('jsch-version', props['jsch.version'].text())
                p.put('gson-version', props['gson.version'].text())
                p.put('http-client-version', props['httpclient.version'].text())
                p.put('commons-cli-version', props['commons.cli.version'].text())
                p.put('commons-codec-version', props['commons.codec.version'].text())
                p.put('commons-io-version', props['commons.io.version'].text())
                p.put('commons-lang-version', props['commons.lang.version'].text())
                p.put('commons-logging-version', props['commons.logging.version'].text())
                p.put('hive-version', props['sdk.hive.version'].text())
            }
        }
        outputFile.withOutputStream { s ->
            p.store(s, null)
        }
    }
}

uploadArchives {
    repositories {
        mavenDeployer {
            configuration = project.configurations.deployerJars
            repository id: 'com.asakusafw.releases', url: 's3://asakusafw/maven/releases'
            snapshotRepository id: 'com.asakusafw.snapshots', url: 's3://asakusafw/maven/snapshots'
        }
    }
}

if (GradleVersion.current() >= GradleVersion.version('2.0')) {
    logger.lifecycle "introducing backport-compat23 (${GradleVersion.current()})"
    configurations {
        backports
    }
    dependencies {
        backports "org.codehaus.groovy:groovy-backports-compat23:${GroovySystem.version}"
    }
    jar {
        from({ configurations.backports.collect { zipTree(it) } }) {
            exclude '**/META-INF/MANIFEST.MF'
        }
    }
}

task sourcesJar(type: Jar) {
    classifier 'sources'
    from sourceSets.main.allSource
}

task testsJar(type: Jar) {
    classifier 'tests'
    from sourceSets.test.output
}

artifacts {
    archives tasks.sourcesJar
    archives tasks.testsJar
}

tasks.withType(Wrapper) {
    jarFile file('.buildtools/gradlew.jar')
}
